apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion androidCompileSdkVersion
    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//region PUBLISH TO JCENTER & MAVEN CENTRAL
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.mikhaellopez'
            artifactId 'circularfillableloaders'
            version androidVersionName
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/circularfillableloaders-release.aar")
        }
    }
}

// Bintray config
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['MyPublication']
    pkg {
        repo = "maven"
        name = "com.mikhaellopez:circularfillableloaders"
        desc = 'This is an Android project allowing to realize a beautiful circular fillable loaders to be used for splashscreen for example.'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lopspower/CircularFillableLoaders.git'
        version {
            name = androidVersionName
            vcsTag = androidVersionName
            released = new Date()
        }
    }
}
//endregion